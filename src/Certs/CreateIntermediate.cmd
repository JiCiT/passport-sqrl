@rem Creates an intermediate cert key pair signed with RootCert.
@rem
@rem IMPORTANT: For non-test purposes the key must be protected offline - do NOT
@rem check into source control, instead place on a USB stick that is kept offline
@rem except for rare cases where it is used to create intermediate certs.
@rem
@rem Usage: CreateIntermediate "Subject String" "BaseFilename"
@rem E.g.: CreateIntermediate "SQRL Test Site Intermediate" TestSiteIntermediate
#rem Expects RootCert.PrivateKey.pem and RootCert.Cert.pem generated by CreateRoot.cmd to be in the current directory.
@rem
@rem Derived from https://stackoverflow.com/questions/19665863/how-do-i-use-a-self-signed-certificate-for-a-https-node-js-server#24749608 and
@rem https://jamielinux.com/docs/openssl-certificate-authority/create-the-root-pair.html
@rem
@rem Assumes OpenSSL-Win64 is installed from https://slproweb.com/products/Win32OpenSSL.html
@rem The "light" version is sufficient. Install binaries into the bin\ directory, not System32.

setlocal ENABLEDELAYEDEXPANSION

set OPENSSL_PATH=c:\OpenSSL-Win64\bin\openssl.exe
set SUBJECT=/C=US/ST=WA/L=Bothell/O=passport-sqrl/CN=%~1
set FILENAME_BASE=%~2
if "%FILENAME_BASE%"=="" set FILENAME_BASE=Intermediate

set ROOT_PRIV=RootCert.PrivateKey.pem
set ROOT_CERT=RootCert.Cert.pem
set INT_PRIV=%FILENAME_BASE%.PrivateKey.pem
set INT_CERT=%FILENAME_BASE%.Cert.pem

@rem Mid key length for intermediate key to last longer against brute force or quantum attack.
%OPENSSL_PATH% genrsa -out %INT_PRIV% 3072
if ERRORLEVEL 1 echo genrsa failed with errorlevel %ERRORLEVEL% && exit /b 1

%OPENSSL_PATH% req -new -key %INT_PRIV% -out %FILENAME_BASE%.csr.pem -subj "%SUBJECT%" -config CertRequestTemplate.cnf -extensions v3_intermediate_ca_policy
if ERRORLEVEL 1 echo Creation of Cert Signing Request failed with errorlevel %ERRORLEVEL% && exit /b 1

%OPENSSL_PATH% x509 -req -in %FILENAME_BASE%.csr.pem -CA %ROOT_CERT% -CAkey %ROOT_PRIV% -CAcreateserial -out %INT_CERT% -days 3650
if ERRORLEVEL 1 echo Creation of intermediate public cert failed with errorlevel %ERRORLEVEL% && exit /b 1

del %FILENAME_BASE%.csr.pem

echo Wrote private key into %INT_PRIV% and cert into %INT_CERT%

exit /b 0
